val diff : List Int -> List Int -> List Int
let rec diff xs ys =
  match xs with
  | Nil -> Nil
  | Cons x xs' ->
    match ys with
    | Nil -> xs
    | Cons y ys' ->
      if x < y then
        Cons x (diff xs' ys)
      else if x == y then
        diff xs' ys'
      else
        diff xs ys'

val ints : List Int
let ints =
  let rec go k = Cons k (go (k+1)) in
  go 1

val solve_aux : List (List Int) -> List (List Int)
let rec solve_aux kss =
  match kss with
  | Nil -> Cons Nil Nil
  | Cons ks kss ->
    concat_map (fun k ->
        map (Cons k)
          (solve_aux (zip_with (fun ls i ->
              diff ls (Cons (k-i) (Cons k (Cons (k+i) Nil))))
            kss
            ints)))
      ks

val solve : Int -> List (List Int)
let solve n = solve_aux (replicate n (take n ints))

val main : IO Unit
let main =
  input
  >>= fun n ->
  print (length (solve n))
