import std/prelude

repeat : List a -> List a
repeat xs =
  let rec ys = append xs ys in
  ys

psums : List Int -> List Int
psums =
  let rec psums0 n xs =
    match xs with
    | Nil -> Nil
    | Cons x xs ->
      let y = x + n in
      Cons y (psums0 y xs)
  in
  psums0 0

filter : (a -> Bool) -> List a -> List a
filter p =
  let rec filter_p xs =
    match xs with
    | Nil -> Nil
    | Cons x xs ->
      let ys = filter_p xs in
      if p x then Cons x ys else ys
  in
  filter_p

sieve : List Int -> List Int
sieve ks =
  match ks with
  | Nil -> abort
  | Cons p ks -> Cons p (sieve (filter (fun k -> k % p != 0) ks))

primes : List Int
primes = Cons 2 (Cons 3 (sieve (psums (Cons 5 (repeat (Cons 2 (Cons 4 Nil)))))))

main : IO Unit
main =
  input
  >>= fun n ->
  print (nth primes n)
